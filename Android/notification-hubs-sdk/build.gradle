plugins {
	id "com.github.dcendents.android-maven" version "1.5"
	id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def libraryVersion = "0.4.1"
def libraryName = "azure-notification-hubs"
def libraryGroup = "com.minyushov.android"
def libraryRepo = "android"
def libraryUrl = "https://github.com/minyushov/azure-notificationhubs"

android {
	compileSdkVersion androidCompileSdkVersion
	buildToolsVersion androidBuildToolsVersion

	defaultConfig {
		minSdkVersion androidMinSdkVersion
		targetSdkVersion androidTargetSdkVersion
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	lintOptions {
		abortOnError false
	}
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])
	compile "com.android.support:appcompat-v7:$androidSupportLibraryVersion"
	compile "com.squareup.okhttp3:okhttp:3.8.0"
}

project.archivesBaseName = libraryName
group = libraryGroup
version = libraryVersion

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
	user = properties.getProperty("bintray.user")
	key = properties.getProperty("bintray.apikey")

	configurations = ['archives']
	dryRun = true

	pkg {
		repo = libraryRepo
		name = libraryName
		vcsUrl = libraryUrl
		licenses = ['Apache-2.0']
	}
}

install {
	repositories.mavenInstaller {
		pom.project {
			packaging 'aar'
			groupId libraryGroup
			artifactId libraryName
			version libraryVersion

			name libraryName
			url libraryUrl

			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				}
			}
			developers {
				developer {
					name 'microsoft'
				}
			}
			scm {
				connection libraryUrl
				url libraryUrl
			}
		}
	}
}

task androidSourcesJar(type: Jar) {
	classifier = 'sources'
	from android.sourceSets.main.java.srcDirs
}

task androidJavadocs(type: Javadoc) {
	source = android.sourceSets.main.java.srcDirs
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
	classifier = 'javadoc'
	from androidJavadocs.destinationDir
}

publishing {
	publications {
		mavenAndroid(MavenPublication) {
			groupId libraryGroup
			artifactId libraryName
			version libraryVersion
			artifact(androidSourcesJar)
			artifact(androidJavadocsJar)
			artifact("$buildDir/outputs/aar/azure-notification-hubs-release.aar")
		}
	}
}

artifacts {
	archives androidSourcesJar
	archives androidJavadocsJar
}